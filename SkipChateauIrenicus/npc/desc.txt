Script files for dealing with skipped NPCs
==========================================

The subfolder "npc" may contain one or more WeiDU script files with .tpa or .tph extension. They are primarily intended 
to deal with NPCs that can potentially be found in Chateau Irenicus, but have been skipped. The mod provides a small 
set of variables that can be used to define script triggers and actions to selected BCS files which deal with the NPCs 
in question.

Note: The mod deals with NPCs in both pre and post 2.0 patched BG2:EE. If the code requires 2.0-specific script commands,
      it is strongly recommended to use a fallback method for older BG2:EE games.

Available variables:

num_npc_actions: (Mandatory) Must be set to the number of desired script blocks to generate. The number should correlate 
                 with the number of defined npc_trigger/npc_action pairs. Empty variables will cause the script generator 
                 to skip the block. Important: You can not define script blocks that depend on variables or actions 
                 defined in a previous block, because each block is executed in the same script pass.

npc_script: (Optional) Use the array variable "npc_script" to define where the associated script trigger/action pair 
            should be added to. You can ignore this variable if your script blocks are to be executed in the Waukeen's 
            Promenade area script (AR0700.BCS).

npc_trigger: (Mandatory) Use the array variable "npc_trigger" to add one or more script trigger blocks that should be 
             evaluated in the area script of Waukeen's Promenade how to handle missed NPCs from Chateau Irenicus. 
             Important: Make sure to avoid syntax errors, or the installation of the mod will fail.

npc_action: (Mandatory) Use the array variable "npc_action" to add one or more script action blocks that are executed 
            in the area script of Waukeen's Promenade which define how to deal with missed NPCs from Chateau Irenicus. 
            Important: Make sure to avoid syntax errors, or the installation of the mod will fail.

trans_action: (Optional) The content of this variable is added to the transition script which is used to transport the 
              party to the surface. You can only set a sequence of actions without defining any triggers. 
              Important: The script actions should not add any noticeable delay, so Wait(x) or similar actions are out 
                         of question. Make sure to avoid syntax errors, or the installation of the mod will fail.

The filename without extension is used as part of an area-local guard variable, so that script blocks are not executed 
multiple times.

Example for Jaheira (npc/jaheira.tph):
ACTION_IF (FILE_EXISTS_IN_GAME ~JAHEIR7.CRE~) BEGIN
  // The arrays npc_script, npc_trigger and npc_action are assumed to have two elements
  OUTER_SET num_npc_actions = 2

  // Skipped elements for this array are assumed to be set to "AR0700.BCS"
  OUTER_TEXT_SPRINT npc_script_1 ~AR0300.BCS~

  // First script trigger, associated with first script action and is added to AR0700.BCS by default
  OUTER_TEXT_SPRINT npc_trigger_0
    ~!InParty("Jaheira")
     !BeenInParty("Jaheira")
     Global("JaheiraJerk","GLOBAL",0)
     Global("JaheiraReleased","GLOBAL",0)
     Global("JaheiraHunt","GLOBAL",0)
     NextTriggerObject("Jaheira")
     NumTimesTalkedTo(0)~

  // Second script trigger, associated with second script action and is added to AR0300.BCS as defined above
  // It's a copy of the first trigger since we need to check the same conditions
  OUTER_TEXT_SPRINT npc_trigger_1 ~%npc_trigger_0%~

  // First script action, associated with first script trigger and is added to AR0700.BCS by default
  OUTER_TEXT_SPRINT npc_action_0
    ~MoveGlobal("AR0300","Jaheira",[1257.3007])~

  // Seconbd action, associated with second script trigger and is added to AR0300.BCS as defined above
  OUTER_TEXT_SPRINT npc_action_1
    ~ActionOverride("Jaheira",SetDialog("JAHEIRAP"))
     ActionOverride("Jaheira",SetGlobal("KickedOut","LOCALS",1))
     ActionOverride("Jaheira",SetNumTimesTalkedTo(1))
     ActionOverride("Jaheira",Face(14)~

  // "trans_action" is not used
END

The resulting script blocks:

AR0700.BCS:
IF
  Global("A7-ShortcutEnabled","GLOBAL",1)
  Global("A7-NPC-JAHEIRA","MYAREA",0)
  !InParty("Jaheira")
  !BeenInParty("Jaheira")
  Global("JaheiraJerk","GLOBAL",0)
  Global("JaheiraReleased","GLOBAL",0)
  Global("JaheiraHunt","GLOBAL",0)
  NextTriggerObject("Jaheira")
  NumberOfTimesTalkedTo(0)
THEN
  RESPONSE #100
    SetGlobal("A7-NPC-JAHEIRA","MYAREA",1)
    MoveGlobal("AR0300","Jaheira",[1257.3007])
    Continue()
END

AR0300.BCS:
IF
  Global("A7-ShortcutEnabled","GLOBAL",1)
  Global("A7-NPC-JAHEIRA","MYAREA",0)
  !InParty("Jaheira")
  !BeenInParty("Jaheira")
  Global("JaheiraJerk","GLOBAL",0)
  Global("JaheiraReleased","GLOBAL",0)
  Global("JaheiraHunt","GLOBAL",0)
  NextTriggerObject("Jaheira")
  NumberOfTimesTalkedTo(0)
THEN
  RESPONSE #100
    SetGlobal("A7-NPC-JAHEIRA","MYAREA",1)
    ActionOverride("Jaheira",SetGlobal("KickedOut","LOCALS",1))
    ActionOverride("Jaheira",SetNumTimesTalkedTo(1))
    ActionOverride("Jaheira",Face(SE))
    Continue()
END


Example TP2 code from an imaginary NPC mod:
ACTION_IF (DIRECTORY_EXISTS ~SkipChateauIrenicus/npc~) BEGIN
  COPY ~AnImaginaryMod/SkipNPC/MyImaginaryNPC.tph~ ~SkipChateauIrenicus/npc~
END
