// *** Code for dealing with NPCs in Chateau Irenicus ***

// List of available vanilla NPCs
ACTION_DEFINE_ARRAY vanillaNPCs BEGIN ~jaheira~ ~minsc~ ~yoshimo~ END

MKDIR ~%MOD_FOLDER%/temp~
OUTER_TEXT_SPRINT a7_script_file ~%MOD_FOLDER%/temp/script.tph~

OUTER_SET num_npc_actions = 0
ACTION_BASH_FOR ~%MOD_FOLDER%/npc~ ~^.+\.tp[ah]$~ BEGIN
  // filtering NPCs
  OUTER_SET skipped = 1
  ACTION_IF (vanilla_npc_only) BEGIN
    ACTION_PHP_EACH vanillaNPCs AS _ => npc  BEGIN
      ACTION_IF (skipped != 0 AND ~%BASH_FOR_RES%~ STRING_EQUAL_CASE ~%npc%~) BEGIN
        OUTER_SET skipped = 0
      END
    END
  END ELSE BEGIN
    OUTER_SET skipped = 0
  END

  ACTION_IF (NOT skipped) BEGIN
    // applying addon code
    COPY - ~%BASH_FOR_FILESPEC%~ ~%a7_script_file%~
      TEXT_SPRINT a7_npc_file ~%SOURCE_RES%~
    ACTION_TRY
      REINCLUDE ~%a7_script_file%~

      // dealing with main actions
      OUTER_FOR (a7_idx = 0; a7_idx < num_npc_actions; a7_idx += 1) BEGIN
        ACTION_IF (VARIABLE_IS_SET $npc_trigger(~%a7_idx%~) AND VARIABLE_IS_SET $npc_action(~%a7_idx%~)) BEGIN
          OUTER_TEXT_SPRINT a7_curTrigger $EVAL npc_trigger("%a7_idx%")
          OUTER_TEXT_SPRINT a7_curAction $EVAL npc_action("%a7_idx%")
          OUTER_TEXT_SPRINT a7_curScript ~AR0700.BCS~
          ACTION_IF (VARIABLE_IS_SET $npc_script(~%a7_idx%~)) BEGIN
            OUTER_TEXT_SPRINT scriptRes $EVAL npc_script("%a7_idx%")
            ACTION_IF (NOT ~%scriptRes%~ STRING_EQUAL ~~ AND FILE_EXISTS_IN_GAME ~%scriptRes%~) BEGIN
              OUTER_TEXT_SPRINT a7_curScript ~%scriptRes%~
            END
          END

          ACTION_IF (NOT ~%a7_curTrigger%~ STRING_EQUAL ~~ AND NOT ~%a7_curAction%~ STRING_EQUAL ~~) BEGIN
            <<<<<<<< .../inlined/script.baf
            IF
              Global("A7-ShortcutEnabled", "GLOBAL", 1)
              Global("A7-NPC-%a7_npc_file%", "MYAREA", 0)
              %a7_curTrigger%
            THEN
              RESPONSE #100
                SetGlobal("A7-NPC-%a7_npc_file%", "MYAREA", 1)
                %a7_curAction%
                Continue()
            END
            >>>>>>>>
            EXTEND_TOP ~%a7_curScript%~ ~.../inlined/script.baf~ EVAL
          END
        END
      END

      // dealing with transition actions
      ACTION_IF (VARIABLE_IS_SET trans_action AND NOT ~%trans_action%~ STRING_EQUAL ~~) BEGIN
        COPY_EXISTING ~a7-tele1.dlg~ ~override~
                      ~a7-tele2.dlg~ ~override~
          DECOMPILE_AND_PATCH BEGIN
            REPLACE_TEXTUALLY ~^[ %TAB%]*\(StartCutScene.*("cut01".*)\)$~ ~%trans_action% \1~
          END
        BUT_ONLY
      END

      // clearing variables
      OUTER_FOR (a7_idx = 0; a7_idx < num_npc_actions; a7_idx += 1) BEGIN
        OUTER_TEXT_SPRINT $EVAL npc_script("%a7_idx%") ~~
        OUTER_TEXT_SPRINT $EVAL npc_trigger("%a7_idx%") ~~
        OUTER_TEXT_SPRINT $EVAL npc_action("%a7_idx%") ~~
      END
      OUTER_TEXT_SPRINT trans_action ~~
      OUTER_SET num_npc_actions = 0
    WITH
      DEFAULT
        WARN ~ADDON ERROR: Syntax error while processing %BASH_FOR_FILESPEC%. Skipping file...~
    END
  END
END

// Removing script from memory
<<<<<<<< .../inlined/script.tph
>>>>>>>>
COPY + ~.../inlined/script.tph~ ~%a7_script_file%~
REINCLUDE ~%a7_script_file%~
DELETE + ~%a7_script_file%~
DELETE + ~%MOD_FOLDER%/temp~
