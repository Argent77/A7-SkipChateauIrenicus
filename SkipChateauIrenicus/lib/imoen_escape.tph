// Script update modes
OUTER_SET OP_REPLACE = 1
OUTER_SET OP_APPEND = 2
OUTER_SET OP_PREPEND = 3
OUTER_SET OP_DELETE = 4

OUTER_SET IS_EE = (GAME_IS ~bgee bg2ee eet iwdee pstee~) ? 1 : 0
ACTION_IF (GAME_IS ~bgt eet~) BEGIN
  OUTER_TEXT_SPRINT imoen ~Imoen2~
END ELSE BEGIN
  OUTER_TEXT_SPRINT imoen ~Imoen~
END

// Search for "find" and return start position as well as position right before next line break
DEFINE_PATCH_FUNCTION FIND_STRING_POS
INT_VAR
  start = 0   // where to start searching
STR_VAR
  find = ~~   // search string
RET
  pos_start
  pos_end
BEGIN
  SET pos_start = "-1"
  SET pos_endn = "-1"
  PATCH_IF (NOT ~%find%~ STR_EQ ~~) BEGIN
    SET pos_start = INDEX_BUFFER(~%find%~ start)
    PATCH_IF (pos_start >= 0) BEGIN
      SET pos_end = INDEX_BUFFER(~[%WNL%]~ pos_start)
    END ELSE BEGIN
      SET pos_start = "-1"
    END
  END
END

// Replace buffer region from "start" to "end" (exclusive) by "replace". Return updated start and end positions.
DEFINE_PATCH_FUNCTION REPLACE_STRING
INT_VAR
  pos_start = "-1"
  pos_end = "-1"
STR_VAR
  replace = ~~
RET
  pos_start pos_end
BEGIN
  PATCH_IF (pos_start >= 0 && pos_end >= pos_start) BEGIN
    SET len1 = pos_end - pos_start
    SET len2 = STRING_LENGTH ~%replace%~
    DELETE_BYTES pos_start len1
    PATCH_IF (len2 > 0) BEGIN
      INSERT_BYTES pos_start len2
      WRITE_ASCIIE pos_start ~%replace%~ (len2)
    END
    SET pos_end = pos_start + len2
  END
END

// Alter script based on specified array data
DEFINE_PATCH_FUNCTION ALTER_SCRIPT
INT_VAR
  data_size = 0 // number of array entries
STR_VAR
  // name of the array containing input, output and operation mode per entry
  //   operation mode key name: op
  //   operation modes: OP_REPLACE, OP_APPEND, OP_PREPEND, OP_DELETE
  //   input key name: in
  //   output key name: out (not needed for OP_DELETE)
  data = ~~
RET success
BEGIN
  SET success = 0
  PATCH_IF (NOT ~%data%~ STR_EQ ~~) BEGIN
    SET success = 1
    SET pos_start = 0
    SET pos_end = 0
    FOR (i = 0; i < data_size && success; ++i) BEGIN
      PATCH_IF (VARIABLE_IS_SET $EVAL ~%data%~(~%i%~ ~in~) &&
                VARIABLE_IS_SET $EVAL ~%data%~(~%i%~ ~op~) &&
                ($EVAL ~%data%~(~%i%~ ~op~) = OP_DELETE || VARIABLE_IS_SET $EVAL ~%data%~(~%i%~ ~out~))) BEGIN
        LPF FIND_STRING_POS INT_VAR start = pos_start STR_VAR find = $EVAL ~%data%~(~%i%~ ~in~) RET pos_start pos_end END
        PATCH_IF (pos_start >= 0) BEGIN
          PATCH_MATCH $EVAL ~%data%~(~%i%~ ~op~) WITH
            OP_REPLACE BEGIN
              LPF REPLACE_STRING INT_VAR pos_start pos_end STR_VAR replace = $EVAL ~%data%~(~%i%~ ~out~) RET pos_start pos_end END
            END
            OP_APPEND BEGIN
              TEXT_SPRINT line $EVAL ~%data%~(~%i%~ ~out~)
              LPF REPLACE_STRING INT_VAR pos_start = pos_end pos_end STR_VAR replace = EVAL ~%LNL%%line%~ RET pos_start pos_end END
            END
            OP_PREPEND BEGIN
              TEXT_SPRINT line $EVAL ~%data%~(~%i%~ ~out~)
              LPF REPLACE_STRING INT_VAR pos_start pos_end = pos_start STR_VAR replace = EVAL ~%line%%LNL%~ RET pos_start pos_end END
            END
            OP_DELETE BEGIN
              LPF REPLACE_STRING INT_VAR pos_start pos_end RET pos_start pos_end END
            END
            DEFAULT
          END
          SET pos_start = pos_end
        END ELSE BEGIN
          SET success = 0
        END
      END ELSE BEGIN
        SET success = 0
      END
    END
  END
END


// Update long cutscene version
DEFINE_PATCH_MACRO UPDATE_COMPLETE
BEGIN
  PATCH_WITH_SCOPE BEGIN
    // lines to patch
    SET wait_time = IS_EE ? 1 : 3   // To work around DisplayString limitation in classic BG2
    DEFINE_ASSOCIATIVE_ARRAY script BEGIN
      ~0~, ~op~ => OP_REPLACE
      ~0~, ~in~ => ~ActionOverride("ishaddud",MoveToPoint~
      ~0~, ~out~ => ~ActionOverride("ishaddud",MoveToPoint([3534.3098]))~

      ~1~, ~op~ => OP_REPLACE
      ~1~, ~in~ => ~[^ %TAB%%WNL%]~
      ~1~, ~out~ => ~ActionOverride("ishaddud",SmallWait(2))~

      ~2~, ~op~ => OP_PREPEND
      ~2~, ~in~ => ~ActionOverride("ishaddud",DisplayString~
      ~2~, ~out~ => ~ActionOverride("ishaddud",MoveToPoint([3432.3028]))
                     ActionOverride("ishaddud",Face(14))
                     ActionOverride("ishaddud",SmallWait(4))~

      ~3~, ~op~ => OP_APPEND
      ~3~, ~in~ => ~CreateVisualEffect("SPFEARE2",~
      ~3~, ~out~ => ~CreateCreatureImpassable("A7CUT602",[3660.3180],0)~

      ~4~, ~op~ => OP_REPLACE
      ~4~, ~in~ => ~ForceSpell("ishaddud",WIZARD_DISINTEGRATE2_IGNORE_RESISTANCE)~
      ~4~, ~out~ => ~SmallWait(40)
                     ActionOverride("A7CUT602",ReallyForceSpell("ishaddud",WIZARD_DISINTEGRATE2_IGNORE_RESISTANCE))~

      ~5~, ~op~ => OP_APPEND
      ~5~, ~in~ => ~PlaySound("EFF_M13")~
      ~5~, ~out~ => ~SmallWait(7)
                     CreateVisualEffect("ICMAGICH",[3396.3050])
                     OpenDoor("CELL06")~

      ~6~, ~op~ => OP_APPEND
      ~6~, ~in~ => ~PlaySound("EFF_P18")~
      ~6~, ~out~ => ~MultiPlayerSync()~

      ~7~, ~op~ => OP_REPLACE
      ~7~, ~in~ => ~ActionOverride("%imoen%",OpenDoor("DOOR11"))~
      ~7~, ~out~ => ~ActionOverride("%imoen%",Face(10))
                     ActionOverride("%imoen%",SmallWait(7))
                     ActionOverride("%imoen%",DisplayStringHead(Myself,@1002))
                     ActionOverride("%imoen%",Wait(%wait_time%))
                     ActionOverride("%imoen%",MoveToPoint([3445.3016]))
                     ActionOverride("%imoen%",SmallWait(8))
                     ActionOverride("%imoen%",Face(14))
                     ActionOverride("%imoen%",SmallWait(8))
                     ActionOverride("%imoen%",Face(6))
                     ActionOverride("%imoen%",SmallWait(8))~

      ~8~, ~op~ => OP_REPLACE
      ~8~, ~in~ => ~CutSceneId(Player4)~
      ~8~, ~out~ => ~CutSceneId("%imoen%")~

      ~9~, ~op~ => OP_REPLACE
      ~9~, ~in~ => ~[^ %TAB%%WNL%]~
      ~9~, ~out~ => ~Wait(3)~

      ~10~, ~op~ => OP_REPLACE
      ~10~, ~in~ => ~Wait(~
      ~10~, ~out~ => ~Wait(3)~
    END

    // automated patching
    LPF ALTER_SCRIPT
      INT_VAR data_size = 11
      STR_VAR data = ~script~
      RET success
    END
    PATCH_IF (NOT success) BEGIN
      PATCH_FAIL ~ERROR: Could not update opening cutscene.~
    END
  END
END


// Update silly short cutscene version
DEFINE_PATCH_MACRO UPDATE_SILLY
BEGIN
  PATCH_WITH_SCOPE BEGIN
    // lines to patch
    DEFINE_ASSOCIATIVE_ARRAY script BEGIN
      ~0~, ~op~ => OP_REPLACE
      ~0~, ~in~ => ~ActionOverride("%imoen%",OpenDoor("DOOR11"))~
      ~0~, ~out~ => ~FadeToColor([30.0],0)
                     Wait(3)
                     CreateCreature("ISHADDUD",[3432.3028],14)
                     FadeFromColor([30.0],0)
                     SmallWait(7)
                     ActionOverride("ishaddud",DisplayStringHead(Myself,54407))
                     ActionOverride("ishaddud",PlaySound("TUTHH04"))
                     SmallWait(7)
                     CreateCreatureImpassable("A7CUT602",[3660.3180],0)
                     ActionOverride("A7CUT602",ReallyForceSpell("ishaddud",WIZARD_DISINTEGRATE2_IGNORE_RESISTANCE))
                     MultiPlayerSync()
                     CreateVisualEffectObject("ICFIRSDI","ishaddud")
                     PlaySound("EFF_M20")
                     Wait(2)
                     CreateVisualEffectObject("SPFIREPI","ishaddud")
                     PlaySound("EFF_M13")
                     SmallWait(7)
                     CreateVisualEffect("ICMAGICH",[3396.3050])
                     OpenDoor("CELL06")
                     ScreenShake([30.30],20)
                     Wait(1)
                     PlaySound("EFF_P18")
                     MultiPlayerSync()
                     Wait(1)
                     ActionOverride("%imoen%",MoveToPoint([3445.3016]))
                     ActionOverride("%imoen%",SmallWait(8))
                     ActionOverride("%imoen%",Face(14))
                     ActionOverride("%imoen%",SmallWait(8))
                     ActionOverride("%imoen%",Face(6))
                     ActionOverride("%imoen%",SmallWait(8))~

      ~1~, ~op~ => OP_REPLACE
      ~1~, ~in~ => ~CutSceneId(Player4)~
      ~1~, ~out~ => ~CutSceneId("%imoen%")~

      ~2~, ~op~ => OP_REPLACE
      ~2~, ~in~ => ~[^ %TAB%%WNL%]~
      ~2~, ~out~ => ~Wait(3)~

      ~3~, ~op~ => OP_REPLACE
      ~3~, ~in~ => ~Wait(~
      ~3~, ~out~ => ~Wait(3)~
    END

    // automated patching
    LPF ALTER_SCRIPT
      INT_VAR data_size = 4
      STR_VAR data = ~script~
      RET success
    END
    PATCH_IF (NOT success) BEGIN
      PATCH_FAIL ~ERROR: Could not update opening cutscene.~
    END
  END
END


// Update non-silly short cutscene version
DEFINE_PATCH_MACRO UPDATE_NONSILLY
BEGIN
  PATCH_WITH_SCOPE BEGIN
    // lines to patch
    DEFINE_ASSOCIATIVE_ARRAY script BEGIN
      ~0~, ~op~ => OP_DELETE
      ~0~, ~in~ => ~Wait(5)~

      ~1~, ~op~ => OP_REPLACE
      ~1~, ~in~ => ~ActionOverride("%imoen%",OpenDoor("DOOR11"))~
      ~1~, ~out~ => ~Wait(2)
                     FadeToColor([30.0],0)
                     Wait(3)
                     CreateCreature("ISHADDUD",[3432.3028],14)
                     FadeFromColor([30.0],0)
                     SmallWait(7)
                     ActionOverride("ishaddud",DisplayStringHead(Myself,54407))
                     ActionOverride("ishaddud",PlaySound("TUTHH04"))
                     SmallWait(7)
                     CreateCreatureImpassable("A7CUT602",[3660.3180],0)
                     ActionOverride("A7CUT602",ReallyForceSpell("ishaddud",WIZARD_DISINTEGRATE2_IGNORE_RESISTANCE))
                     MultiPlayerSync()
                     CreateVisualEffectObject("ICFIRSDI","ishaddud")
                     PlaySound("EFF_M20")
                     Wait(2)
                     CreateVisualEffectObject("SPFIREPI","ishaddud")
                     PlaySound("EFF_M13")
                     SmallWait(7)
                     CreateVisualEffect("ICMAGICH",[3396.3050])
                     OpenDoor("CELL06")
                     ScreenShake([30.30],20)
                     Wait(1)
                     PlaySound("EFF_P18")
                     MultiPlayerSync()
                     Wait(1)
                     ActionOverride("%imoen%",MoveToPoint([3445.3016]))
                     ActionOverride("%imoen%",SmallWait(8))
                     ActionOverride("%imoen%",Face(14))
                     ActionOverride("%imoen%",SmallWait(8))
                     ActionOverride("%imoen%",Face(6))
                     ActionOverride("%imoen%",SmallWait(8))~

      ~2~, ~op~ => OP_REPLACE
      ~2~, ~in~ => ~CutSceneId(Player4)~
      ~2~, ~out~ => ~CutSceneId("%imoen%")~

      ~3~, ~op~ => OP_REPLACE
      ~3~, ~in~ => ~[^ %TAB%%WNL%]~
      ~3~, ~out~ => ~Wait(3)~

      ~4~, ~op~ => OP_REPLACE
      ~4~, ~in~ => ~Wait(~
      ~4~, ~out~ => ~Wait(3)~
    END

    // automated patching
    LPF ALTER_SCRIPT
      INT_VAR data_size = 5
      STR_VAR data = ~script~
      RET success
    END
    PATCH_IF (NOT success) BEGIN
      PATCH_FAIL ~ERROR: Could not update opening cutscene.~
    END
  END
END


DEFINE_ACTION_FUNCTION UPDATE_CUTSCENE
BEGIN
  // Checking for Faster chapter 1&2 cutscenes
  OUTER_SET is_short = 0
  OUTER_SET is_silly = 0
  // Silly version
  ACTION_IF (MOD_IS_INSTALLED ~setup-bg2_tweaks.tp2~ 3140) || (MOD_IS_INSTALLED ~setup-cdtweaks.tp2~ 3140) BEGIN
    OUTER_SET is_short = 1
    OUTER_SET is_silly = 1
  END
  // Non-silly version
  ACTION_IF (MOD_IS_INSTALLED ~setup-bg2_tweaks.tp2~ 3141) || (MOD_IS_INSTALLED ~setup-cdtweaks.tp2~ 3141) BEGIN
    OUTER_SET is_short = 1
  END

  // Fixing party start position
  COPY_EXISTING ~startpos.2da~ ~override~
    COUNT_2DA_COLS num_cols
    PATCH_IF (num_cols > 4) BEGIN
      SET_2DA_ENTRY 3 4 1 ~3830~
      SET_2DA_ENTRY 4 4 1 ~2504~
    END
  BUT_ONLY

  // Fixing Imoen start position
  COPY_EXISTING ~baldur.gam~ ~override~
    READ_LONG 0x30 ofs_npc
    READ_LONG 0x34 num_npc
    FOR (i = 0; i < num_npc; ++i) BEGIN
      SET ofs = ofs_npc + (i * 0x160)
      READ_ASCII (ofs + 0x0c) resref (8) NULL
      PATCH_IF (~%resref%~ STRING_CONTAINS_REGEXP ~IMOEN~ = 0) BEGIN
        READ_ASCII (ofs + 0x18) resref (8) NULL
        PATCH_IF (~%resref%~ STR_EQ ~AR0602~) BEGIN
          WRITE_LONG (ofs + 0x14) 10  // Direction
          WRITE_SHORT (ofs + 0x20) 3374 // X
          WRITE_SHORT (ofs + 0x22) 3068 // Y
          SET i = num_npc
        END
      END
    END
  BUT_ONLY

  // Game-specific preparations
  ACTION_IF (GAME_IS ~bgt~) BEGIN
    COPY_EXISTING ~ar0602.bcs~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~CreateCreature("\(Imoen[0-9]*\)",\[[0-9]+\.[0-9]+\],[0-9A-Z]+)~
                          ~CreateCreature("\1",[0.0],0)%LNL%ActionOverride("%imoen%",JumpToPoint([3374.3068]))%LNL%ActionOverride("%imoen%",Face(10))~
      END
    BUT_ONLY

    COPY_EXISTING ~aram00.bcs~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~ActionOverride("%imoen%",MoveBetweenAreas("AR0602",\[[0-9]+\.[0-9]+\],[0-9]+))~
                          ~ActionOverride("%imoen%",MoveBetweenAreas("AR0602",[3374.3068],10))~
      END
    BUT_ONLY

    COPY_EXISTING ~amnscene.bcs~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~ActionOverride(Player4,LeaveAreaLUA("AR0602","",\[[0-9]+\.[0-9]+\],[0-9]+))~
                          ~ActionOverride(Player4,LeaveAreaLUA("AR0602","",[3830.2504],12))~
      END
    BUT_ONLY
  END ELSE ACTION_IF (GAME_IS ~eet~) BEGIN
    COPY_EXISTING ~k#telbgt.bcs~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~ActionOverride(Player4,LeaveAreaLUA("AR0602","",\[[0-9]+\.[0-9]+\],[0-9A-Z]+))~
                          ~ActionOverride(Player4,LeaveAreaLUA("AR0602","",[3830.2504],NNE))~
      END
    BUT_ONLY
    COPY_EXISTING ~ar0602.bcs~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~CreateCreature("\(Imoen[0-9]+\)",\[[0-9]+\.[0-9]+\],[0-9A-Z]+)~
                          ~CreateCreature("\1",[0.0],S)%LNL%ActionOverride("%imoen%",JumpToPoint([3374.3068]))%LNL%ActionOverride("%imoen%",Face(NE))~
        REPLACE_TEXTUALLY ~MoveGlobal("AR0602","%imoen%",\[[0-9]+\.[0-9]+\])~
                          ~MoveGlobal("AR0602","%imoen%",[3374.3068])~
        REPLACE_TEXTUALLY ~ActionOverride(Player4,LeaveAreaLUA("AR0602","",\[[0-9]+\.[0-9]+\],[0-9A-Z]+))~
                          ~ActionOverride(Player4,LeaveAreaLUA("AR0602","",[3830.2504],NNE))~
      END
    BUT_ONLY
  END

  // Creating cutspy clone
  COPY_EXISTING ~m05pcspy.cre~ ~override/a7cut602.cre~
    WRITE_ASCII 0x280 ~A7CUT602~ (32)
    LPF DELETE_CRE_EFFECT
      INT_VAR opcode_to_delete = 268  // Clear Fog of War
    END

  // Updating skipped cutscene cleanup script
  COPY_EXISTING ~cutskip2.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      SET pos1 = INDEX_BUFFER(~RESPONSE #1$~)
      SET pos2 = INDEX_BUFFER(~RESPONSE #2$~ pos1)
      PATCH_IF (pos1 > 0 && pos2 < pos1) BEGIN
        LPF FIND_STRING_POS STR_VAR find = ~OpenDoor("DOOR11")~ RET pos_start pos_end END
        PATCH_IF (pos_start > pos1 && pos_start < pos2) BEGIN
          LPF REPLACE_STRING INT_VAR pos_start = pos pos_end = pos + search_len STR_VAR replace = EVAL ~OpenDoor("CELL06")~ END
        END
      END
    END
  BUT_ONLY IF_EXISTS

  // Updating opening cutscene script
  COPY_EXISTING ~newgame.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      PATCH_IF (is_short) BEGIN
        PATCH_IF (is_silly) BEGIN
          LPM UPDATE_SILLY
        END ELSE BEGIN
          LPM UPDATE_NONSILLY
        END
      END ELSE BEGIN
        LPM UPDATE_COMPLETE
      END
    END
  BUT_ONLY
END
