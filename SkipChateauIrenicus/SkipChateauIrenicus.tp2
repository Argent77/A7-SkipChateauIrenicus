BACKUP ~weidu_external/backup/SkipChateauIrenicus~
SUPPORT ~http://www.shsforums.net/forum/655-skip-chateau-irenicus/~
VERSION ~3.1~

ALWAYS
  // required because of non-standard BACKUP path
  OUTER_TEXT_SPRINT MOD_FOLDER ~SkipChateauIrenicus~
  // root folder for mutable data
  OUTER_TEXT_SPRINT WORK_FOLDER ~weidu_external~

  OUTER_SET IS_EE = (GAME_IS ~bg2ee eet~) ? 1 : 0
  OUTER_SET IS_EE2 = (IS_EE != 0 AND FILE_EXISTS ~engine.lua~) ? 1 : 0

  ACTION_DEFINE_ARRAY a7noconvert BEGIN ~setup~ END
  ACTION_DEFINE_ARRAY a7reload BEGIN ~mod~ END
  LAF HANDLE_CHARSETS
    INT_VAR
      from_utf8         = 1
      infer_charset     = 1
    STR_VAR
      default_language  = ~english~
      tra_path          = EVAL ~%MOD_FOLDER%/languages~
      out_path          = EVAL ~%WORK_FOLDER%/lang/%MOD_FOLDER%~
      noconvert_array   = ~a7noconvert~
      reload_array      = ~a7reload~
  END
END

README ~SkipChateauIrenicus/readme/readme-%LANGUAGE%.html~
       ~SkipChateauIrenicus/readme/readme-%LANGUAGE%.txt~
       ~SkipChateauIrenicus/readme/readme.html~
       ~SkipChateauIrenicus/readme/readme.txt~

LANGUAGE ~English~ ~english~
         ~SkipChateauIrenicus/languages/english/setup.tra~
         ~SkipChateauIrenicus/languages/english/mod.tra~
LANGUAGE ~Deutsch~ ~german~
         ~SkipChateauIrenicus/languages/english/setup.tra~
         ~SkipChateauIrenicus/languages/english/mod.tra~
         ~SkipChateauIrenicus/languages/german/setup.tra~
         ~SkipChateauIrenicus/languages/german/mod.tra~
LANGUAGE ~Francais (Traduction: Gwendolyne)~ ~french~
         ~SkipChateauIrenicus/languages/english/setup.tra~
         ~SkipChateauIrenicus/languages/english/mod.tra~
         ~SkipChateauIrenicus/languages/french/setup.tra~
         ~SkipChateauIrenicus/languages/french/mod.tra~
LANGUAGE ~Polski (Tlumaczenie: Cahir)~ ~polish~
         ~SkipChateauIrenicus/languages/english/setup.tra~
         ~SkipChateauIrenicus/languages/english/mod.tra~
         ~SkipChateauIrenicus/languages/polish/setup.tra~
         ~SkipChateauIrenicus/languages/polish/mod.tra~
LANGUAGE ~Russian (Translation: Austin)~ ~russian~
         ~SkipChateauIrenicus/languages/english/setup.tra~
         ~SkipChateauIrenicus/languages/english/mod.tra~
         ~SkipChateauIrenicus/languages/russian/setup.tra~
         ~SkipChateauIrenicus/languages/russian/mod.tra~

BEGIN @0  // Skip Chateau Irenicus
  REQUIRE_PREDICATE GAME_IS ~bg2 tob bgt bg2ee eet~ @100 // This mod requires BG2, BGT, BG2:EE or EET.
  DESIGNATED 0

  ACTION_IF IS_EE BEGIN
    INCLUDE ~%MOD_FOLDER%/lib/journal.tph~
  END
  INCLUDE ~%MOD_FOLDER%/lib/tables.tph~
  INCLUDE ~%MOD_FOLDER%/lib/portal.tph~
  INCLUDE ~%MOD_FOLDER%/lib/rogue.tph~
  INCLUDE ~%MOD_FOLDER%/lib/items.tph~
  INCLUDE ~%MOD_FOLDER%/lib/misc.tph~


BEGIN @11 // Vanilla NPCs only
  REQUIRE_COMPONENT ~SkipChateauIrenicus.tp2~ 0 @102  // Main component required.
  REQUIRE_PREDICATE GAME_IS ~bg2ee eet~ @101 // This component requires BG2:EE or EET.
  SUBCOMPONENT @10  // Deal with skipped NPCs
  DESIGNATED 101

  OUTER_SET vanilla_npc_only = 1
  INCLUDE ~%MOD_FOLDER%/lib/npcs.tph~


BEGIN @12 // All available NPCs
  REQUIRE_COMPONENT ~SkipChateauIrenicus.tp2~ 0 @102  // Main component required.
  REQUIRE_PREDICATE GAME_IS ~bg2ee eet~ @101 // This component requires BG2:EE or EET.
  SUBCOMPONENT @10  // Deal with skipped NPCs
  DESIGNATED 102

  OUTER_SET vanilla_npc_only = 0
  INCLUDE ~%MOD_FOLDER%/lib/npcs.tph~
